plugins {
    id 'java'
    id 'war'
}

group 'mis283'
sourceCompatibility = 1.8

apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'

repositories {
    mavenCentral()
}

sourceSets {
    integration {
        java.srcDir 'src/integration/java'
        resources.srcDir 'src/integration/resources'
    }
}

gretty {
    httpPort = 8080
    integrationTestTask = 'integration'
}

dependencies {
    compile 'javax.ws.rs:javax.ws.rs-api:2.1'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'org.glassfish.jersey.core:jersey-server:2.26'
    compile 'org.hibernate:hibernate-core:5.2.11.Final'
    testCompile 'junit:junit:4.12'

    runtime 'org.glassfish.jersey.containers:jersey-container-servlet:2.26'
    runtime 'org.glassfish.jersey.core:jersey-common:2.26'
    runtime 'org.glassfish.jersey.inject:jersey-hk2:2.26'
    runtime 'org.glassfish.jersey.media:jersey-media-json-jackson:2.26'
    runtime 'com.h2database:h2:1.4.196'

    integrationCompile sourceSets.main.output
    integrationCompile configurations.compile
    integrationCompile configurations.testCompile

    integrationRuntime configurations.runtime
    integrationRuntime configurations.testRuntime
}

task integration(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the integration tests.'

    maxHeapSize = '256m'

    testClassesDir = sourceSets.integration.output.classesDir
    classpath = sourceSets.integration.runtimeClasspath

    binResultsDir = file("$buildDir/integration-test-results/binary/integration")

    reports {
        html.destination = "$buildDir/reports/integration-test"
        junitXml.destination = "$buildDir/integration-test-results"
    }

    mustRunAfter tasks.test
}

// To rerun all tests and not just failures or updates
test.dependsOn 'cleanTest'
// To rerun all integration tests and not just failures or updates
integration.dependsOn 'cleanIntegration'
check.dependsOn integration